// Copyright (c) 2019 - 2022 RacoonStudios

//[-------------------------------------------------------]
//[ Definitions                                           ]
//[-------------------------------------------------------]
@includepiece(../Shared/SP_Core.asset)
	@insertpiece(SetCrossPlatformSettings)


//[-------------------------------------------------------]
//[ Input / output                                        ]
//[-------------------------------------------------------]
// Attribute input / output
INPUT_BEGIN
	DECLARE_GLOBAL_INVOCATION_ID
INPUT_END

// Uniform buffers
struct PassDataStruct
{
	uint3  GlobalComputeSize;
	float  NearBlurDepth;
	float2 ProjectionParameters;
	float  FocalPlaneDepth;
	float  FarBlurDepth;
	float  BlurrinessCutoff;	// Blurriness cutoff constant for objects behind the focal plane
};
UNIFORM_BUFFER_BEGIN(0, 0, PassUniformBuffer, 0)
	PassDataStruct PassData;
UNIFORM_BUFFER_END

// Textures
TEXTURE_2D(1, 0, ColorMap, 0)
TEXTURE_2D(1, 1, DepthMap, 1)
TEXTURE_2D(1, 2, BlurMap, 2)	// Blurred low resolution version of the color map
RW_TEXTURE_2D(1, 3, OutputTexture2D, 3, r11f_g11f_b10f, writeonly)

// Samplers
SAMPLER_STATE(2, 0, SamplerPointClamp, 0)
SAMPLER_STATE(2, 1, SamplerLinear, 1)


//[-------------------------------------------------------]
//[ Functions                                             ]
//[-------------------------------------------------------]
@includepiece(../Shared/SP_Depth.asset)
	@insertpiece(DefineGetLinearDepth)

float ConvertDepth(float linearDepth)
{
	float depth = 0.0f;
	FLATTEN if (linearDepth < PassData.FocalPlaneDepth)
	{
		// Scale depth value between near blur distance and focal distance to [-1, 0] range
		depth = (linearDepth - PassData.FocalPlaneDepth) / (PassData.FocalPlaneDepth - PassData.NearBlurDepth);
	}
	else
	{
		// Scale depth value between focal distance and far blur distance to [0, 1] range
		depth = (linearDepth - PassData.FocalPlaneDepth) / (PassData.FarBlurDepth - PassData.FocalPlaneDepth);

		// Clamp the far blur to a maximum blurriness
		depth = clamp(depth, 0.0f, PassData.BlurrinessCutoff);
	}

	// Scale and bias into [0, 1] range
	depth = 0.5f * depth + 0.5f;
	return depth;
}

float4 CalculateSum(float2 textureCoordinate,
					float2 pixelSizeScene)	// Pixel size of full resolution image
{
	// Depth of field basing on https://developer.amd.com/wordpress/media/2012/10/Scheuermann_DepthOfField.pdf
	float2 pixelSizeBlur = float2(1.0f, 1.0f) / CAST_TO(GET_TEXTURE_2D_LOD_SIZE(BlurMap, 0), float2);	// Pixel size of downsampled and blurred image
	float2 poisson[12];	// Contains poisson-distributed positions on the unit circle for 12 taps
	poisson[ 0] = float2( 0.00f,  0.00f);
	poisson[ 1] = float2( 0.07f, -0.45f);
	poisson[ 2] = float2(-0.15f, -0.33f);
	poisson[ 3] = float2( 0.35f, -0.32f);
	poisson[ 4] = float2(-0.39f, -0.26f);
	poisson[ 5] = float2( 0.10f, -0.23f);
	poisson[ 6] = float2( 0.36f, -0.12f);
	poisson[ 7] = float2(-0.31f, -0.01f);
	poisson[ 8] = float2(-0.38f,  0.22f);
	poisson[ 9] = float2( 0.36f,  0.23f);
	poisson[10] = float2(-0.13f,  0.29f);
	poisson[11] = float2( 0.14f,  0.41f);

	// Maximum circle of confusion (CoC) radius and diameter in pixels
	float2 maxCoC = float2(5.0f, 10.0f);

	// Scale factor for minimum CoC size on low resolution image
	float radiusScale = 0.4f;

	// Get depth of center tap and convert it into blur radius in pixels
	float centerDepth	  = ConvertDepth(GetLinearDepth(SAMPLE_DEPTH_2D_LOD(DepthMap, SamplerPointClamp, float4(textureCoordinate, 0.0f, 0.0f))));
	float discRadiusScene = abs(centerDepth * maxCoC.y - maxCoC.x);
	float discRadiusBlur  = discRadiusScene * radiusScale;	// Radius on low resolution image

	float4 sum = float4(0.0f, 0.0f, 0.0f, 0.0f);
	UNROLL for (int i = 0; i < 12; ++i)
	{
		// Compute texture coordinates
		float4 coordScene = float4(textureCoordinate + (pixelSizeScene * poisson[i] * discRadiusScene), 0.0f, 0.0f);
		float4 coordBlur = float4(textureCoordinate + (pixelSizeBlur * poisson[i] * discRadiusBlur), 0.0f, 0.0f);

		// Fetch taps and depth
		float4 tapScene = SAMPLE_2D_LOD(ColorMap, SamplerPointClamp, coordScene);
		float tapDepth = ConvertDepth(GetLinearDepth(SAMPLE_DEPTH_2D_LOD(DepthMap, SamplerPointClamp, float4(coordScene.xy, 0.0f, 0.0f))));
		float4 tapBlur = SAMPLE_2D_LOD(BlurMap, SamplerLinear, coordBlur);

		// Mix low and high resolution taps based on tap blurriness
		float blurAmount = abs(tapDepth * 2.0f - 1.0f);	// Put blurriness into [0, 1]
		float4 tap = LERP(tapScene, tapBlur, blurAmount);

		// "smart" blur ignores taps that are closer than the center tap and in focus
		float factor = (tapDepth >= centerDepth) ? 1.0f : abs(tapDepth * 2.0f - 1.0f);

		// Accumulate
		sum.rgb += tap.rgb * factor;
		sum.a += factor;
	}

	// Done
	return sum;
}


//[-------------------------------------------------------]
//[ Main                                                  ]
//[-------------------------------------------------------]
MAIN_BEGIN_COMPUTE(32, 32, 1)
	// Compute dispatch is using groups of a multiple of the local size, ensure we don't access out of global compute size which mustn't be a multiple of the local size and hence might be smaller
	BRANCH if (GLOBAL_INVOCATION_ID.x < PassData.GlobalComputeSize.x && GLOBAL_INVOCATION_ID.y < PassData.GlobalComputeSize.y)
	{
		float2 inverseGlobalComputeSize = float2(1.0f, 1.0f) / CAST_TO(PassData.GlobalComputeSize.xy, float2);
		float4 sum = CalculateSum((CAST_TO(GLOBAL_INVOCATION_ID.xy, float2) + float2(0.5f, 0.5f)) * inverseGlobalComputeSize, inverseGlobalComputeSize);

		// Done, ensure we're in a 0..1 range
		IMAGE_STORE(OutputTexture2D, CAST_TO(GLOBAL_INVOCATION_ID.xy, int2), float4(sum.rgb / sum.a, 1.0f));
	}
MAIN_END_NO_OUTPUT
